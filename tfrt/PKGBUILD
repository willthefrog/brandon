# -*- mode: sh; tab-width: 2; -*-
# Maintainer: Yang Zhang <yangzhang@live.com>

pkgbase=tfrt
pkgname=tfrt-git
pkgver=0.1.39
pkgrel=1
pkgdesc='A performant and modular runtime for TensorFlow.'
arch=('x86_64')
url='https://github.com/tensorflow/runtime'
license=('Apache')
makedepends=(gcc clang bazel)
provides=(tfrt)
conflicts=(tfrt)
replaces=(tfrt)
source=("${pkgbase}::git+${url}.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgbase}"
  echo "$(git log -1 --format="%cd" --date=short | sed 's|-||g').r$(git rev-list --count HEAD)".$(git describe --always | sed 's/.*-//g')
}

build() {
  cd "${pkgbase}"
  bazel build -c opt //tools:bef_executor
  bazel build -c opt //tools:tfrt_translate
}

package() {
  cd "${pkgbase}"
  install -d "${pkgdir}/usr/bin/"
  install -Dm755 bazel-bin/tools/tfrt_translate "${pkgdir}/usr/bin/tfrt_translate"
  install -Dm755 bazel-bin/tools/bef_executor "${pkgdir}/usr/bin/bef_executor"
}

# vim: set ts=2 sw=2:
