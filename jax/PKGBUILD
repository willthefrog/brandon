# -*- mode: sh-mode; tab-width: 2; -*-
# Maintainer: Yang Zhang <yangzhang@live.com>

pkgbase=jax
pkgname=(python-jax-git python-jaxlib-cuda-git)
pkgver=0.1.39
pkgrel=1
arch=('x86_64')
url='https://github.com/google/jax'
license=('Apache')
makedepends=(git gcc bazel cuda cudnn nccl python-setuptools python-scipy cython)
depends=(absl-py python-numpy)
source=("${pkgbase}::git+${url}.git"
        "grpc.patch::https://raw.githubusercontent.com/clearlinux-pkgs/tensorflow/master/Add-grpc-fix-for-gettid.patch")
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd "${pkgbase}"
  echo "$(git log -1 --format="%cd" --date=short | sed 's|-||g').r$(git rev-list --count HEAD)".$(git describe --always | sed 's/.*-//g')
}

prepare() {
  cd "${pkgbase}"
  mkdir -p external
  cp ../grpc.patch external/

  patch -p1  << __EOF__
--- a/WORKSPACE
+++ b/WORKSPACE
@@ -33,6 +33,9 @@ http_archive(
     urls = [
         "https://github.com/tensorflow/tensorflow/archive/210649dd56d7c4b75e3e8e2a851b61c80ae13dbb.tar.gz",
     ],
+    patch_tool="patch",
+    patches=["grpc.patch"],
+    patch_args=["-p1"],
 )

 # For development, one can use a local TF repository instead.

--- a/build/build.py
+++ b/build/build.py
@@ -341,7 +341,6 @@ def main():
     [":install_xla_in_source_tree", os.getcwd()])
   print(" ".join(command))
   shell(command)
-  shell([bazel_path, "shutdown"])
__EOF__
}


build() {
  cd "${pkgbase}"

  export CCACHE_DISABLE=1

  if [ -e '/opt/cuda/include/cudnn.h' ]; then
    cudnn_path='/opt/cuda'
  else
    cudnn_path='/usr'
  fi
  if [ -e '/opt/cuda/include/nccl.h' ]; then
    nccl_header='/opt/cuda/include/nccl.h'
  else
    nccl_header='/usr/include/nccl.h'
  fi

  TF_CUDA_COMPUTE_CAPABILITIES=6.1,7.0                                                             \
  TF_CUDA_PATHS=/opt/cuda,/usr/lib,/usr                                                            \
  TF_NCCL_VERSION=$(sed -n 's/^#define NCCL_MAJOR\s*\(.*\).*/\1/p' ${nccl_header})                 \
  python build/build.py --bazel_path=`which bazel` --enable_cuda --cuda_path=/opt/cuda --cudnn_path=${cudnn_path} --bazel_startup_options='--max_idle_secs=300'
}

package_python-jax-git() {
  pkgdesc='Differentiate, compile, and transform Numpy code.'
  depends+=(python-jaxlib python-opt_einsum)
  provides=(python-jax)
  conflicts=(python-jax)
  replaces=(python-jax)

  cd "${pkgbase}"
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python-jaxlib-cuda-git() {
  pkgdesc='XLA library for JAX',
  depends+=(cuda cudnn nccl absl-py python-protobuf python-scipy)
  provides=(python-jaxlib)
  conflicts=(python-jaxlib)
  replaces=(python-jaxlib)

  cd "${pkgbase}/build"
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim: set ts=2 sw=2:
