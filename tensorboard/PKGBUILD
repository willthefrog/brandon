# Maintainer: Yang Zhang <yangzhang@live.com>

pkgbase=tensorboard
pkgname=python-tensorboard-git
pkgver=0.1.3.r642.g08a157f4
pkgrel=1
pkgdesc="TensorFlow's Visualization Toolkit."
arch=('x86_64')
url="https://github.com/tensorflow/tensorboard"
license=('APACHE')
makedepends=('git' 'bazel' 'python-setuptools' 'tree')
depends=('python' 'python-werkzeug' 'python-numpy' 'python-bleach' 'python-wheel' 'python-markdown' 'python-tensorflow-serving-api'
         'python-html5lib' 'python-protobuf' 'python-setuptools' 'python-tensorflow' 'python-grpcio' 'python-google-auth-oauthlib')
source=("${pkgbase}::git+${url}.git")
md5sums=('SKIP')

pkgver() {
  cd "$pkgbase"
  echo "$(git log -1 --format="%cd" --date=short | sed 's|-||g').r$(git rev-list --count HEAD)".$(git describe --always | sed 's/.*-//g')
}


prepare() {
  cd "$pkgbase"
  # XXX patch to fix bazel 5.0 build, see https://github.com/tensorflow/tensorboard/issues/5513
  patch -p1  <<__EOF__
--- a/WORKSPACE
+++ b/WORKSPACE
@@ -2,6 +2,15 @@ workspace(name = "org_tensorflow_tensorboard")

 load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

+http_archive(
+  name = "build_bazel_rules_apple",
+  sha256 = "0052d452af7742c8f3a4e0929763388a66403de363775db7e90adecb2ba4944b",
+  urls = [
+      "http://mirror.tensorflow.org/github.com/bazelbuild/rules_apple/releases/download/0.31.3/rules_apple.0.31.3.tar.gz",
+      "https://github.com/bazelbuild/rules_apple/releases/download/0.31.3/rules_apple.0.31.3.tar.gz",
+  ],
+)
+
 http_archive(
     name = "io_bazel_rules_webtesting",
     sha256 = "9bb461d5ef08e850025480bab185fd269242d4e533bca75bfb748001ceb343c3",
__EOF__
}


build() {
  cd "$pkgbase"

  export PYTHONWARNINGS="ignore"  # XXX disable `disutils` deprecation warning in python 3.10
  bazel build tensorboard:tensorboard
  bazel build //tensorboard/pip_package:build_pip_package

  rm -rf pypackage
  mkdir pypackage
  cp -R bazel-bin/tensorboard/pip_package/build_pip_package.runfiles/org_tensorflow_tensorboard/external pypackage
  cp -R bazel-bin/tensorboard/pip_package/build_pip_package.runfiles/org_tensorflow_tensorboard/tensorboard pypackage
  cp tensorboard/pip_package/{README.rst,MANIFEST.in} pypackage
  cp README.md pypackage
  cp tensorboard/pip_package/setup.py pypackage
  cp tensorboard/pip_package/requirements.txt pypackage
  cd pypackage
  python setup.py build
}

package() {
  cd "$pkgbase"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd pypackage
  python setup.py install --root="$pkgdir" --optimize=1

  _pyver="$(python -c 'import sys; print("%s.%s" %sys.version_info[0:2])')"
  rm -r "$pkgdir"/usr/lib/python${_pyver}/site-packages/external/

  _requires="$pkgdir"/usr/lib/python${_pyver}/site-packages/tensorboard-*.egg-info/requires.txt
  sed -i '/^futures/d' ${_requires}
  # make it `tensorboard-plugin-wit` optional dep
  sed -i '/^tensorboard-plugin-wit/d' ${_requires}
}

# vim:set ts=2 sw=2 et:
