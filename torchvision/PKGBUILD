# -*- mode: sh; tab-width: 2; -*-
# Maintainer: Yang Zhang <yangzhang@live.com>
# Upstream URL: https://github.com/pytorch/vision

pkgbase=torchvision
pkgname=python-torchvision-cuda-git
pkgver=0.2.1.r29.gfe973ce
pkgrel=1
pkgdesc="Datasets, Transforms and Models specific to Computer Vision"
arch=('x86_64')
url="https://github.com/pytorch/vision"
license=('BSD')
provides=('python-torchvision')
conflicts=('python-torchvision')
replaces=('python-torchvision')
depends=('python-torch' 'python-six' 'python-pillow')
makedepends=('git' 'cmake' 'ninja' 'cuda' 'python-setuptools')
source=("${pkgbase}::git+$url.git")
md5sums=('SKIP')

pkgver() {
  cd "$pkgbase"
  echo "$(git log -1 --format="%cd" --date=short | sed 's|-||g').r$(git rev-list --count HEAD)".$(git describe --always | sed 's/.*-//g')
}

prepare() {
  cd "$pkgbase"
  git submodule update --init --recursive

  patch -p1  << __EOF__
--- a/setup.py
+++ b/setup.py
@@ -299,8 +299,7 @@ def get_extensions():
                     png_bin_folder = os.path.dirname(libpng)
                     libpng_on_conda = (
                         running_under_conda and bin_folder == png_bin_folder)
-                    release_info = get_linux_distribution()
-                    not_debian = release_info["NAME"] not in {'Ubuntu', 'Debian'}
+                    not_debian = True
                 if not linux or libpng_on_conda or not_debian:
                     png_lib = subprocess.run([libpng, '--libdir'],
                                              stdout=subprocess.PIPE)
__EOF__
}

build() {
  cd "$pkgbase"
  MAX_JOBS=`nproc` \
  CC=/opt/cuda/bin/gcc \
  CXX=/opt/cuda/bin/g++ \
  FORCE_CUDA=1 \
  TORCH_CUDA_ARCH_LIST="5.0+PTX;6.1;7.0" \
  python setup.py build
}

package() {
  cd "$pkgbase"
  python setup.py install --skip-build --prefix=/usr --root="$pkgdir" --optimize=1
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: set ts=2 sw=2:
